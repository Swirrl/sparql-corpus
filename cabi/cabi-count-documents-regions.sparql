PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX fao: <http://www.fao.org/countryprofiles/geoinfo/geopolitical/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX linkeddev: <http://linked-development.org/dev/>
PREFIX dbpo: <http://dbpedia.org/ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?countable ?countableId ?countableName (COUNT(DISTINCT ?document) AS ?count) WHERE {
  GRAPH <http://linked-development.org/graph/eldis> {  
    {
     ?document a bibo:Article ;
               a ?articleType ;
               dcterms:coverage ?countable .
    
     ?countable a fao:geographical_region ;
                rdfs:label ?countableName ;
                dcterms:identifier ?countableId .

    } UNION {
      SELECT ?document ("" AS ?countable) ("" AS ?countableId)  ("" AS ?countableName) WHERE {
        ?document a bibo:Article ;
                  a ?articleType .
      
        FILTER NOT EXISTS {
          ?document dcterms:coverage ?countable .
        }
      }
    }
  }
} GROUP BY ?countable ?countableId ?countableName  
